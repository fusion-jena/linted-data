#
# Copyright 2022 Merle Gänßinger (merle.gaenssinger@uni-jena.de)
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?property ?error
WHERE
{
    {
    SELECT ?property ?numberOfRanges ?numberOfDomains ?domains ?ranges
    WHERE
    { # find all properties that have more than one defined domain or range
      OPTIONAL { # find all properties and their defined domains
        SELECT ?property (GROUP_CONCAT(?domain ; separator=", ") as ?domains) (COUNT(?domain) AS ?numDomains)
        WHERE
        {
          SELECT ?property ?domain
          WHERE {
              ?property rdfs:domain ?domain
          }
          ORDER BY ?domain
        }
        GROUP BY ?property
      }
      OPTIONAL { # find all properties and their defined ranges
        SELECT ?property (GROUP_CONCAT(?range ; separator=", ") as ?ranges) (COUNT(?range) AS ?numRanges)
        WHERE
        {
          SELECT ?property ?range
          WHERE {
              ?property rdfs:range ?range
          }
          ORDER BY ?range
        }
        GROUP BY ?property
      }
      BIND ( IF(BOUND(?numDomains), ?numDomains, 0) AS ?numberOfDomains).
      BIND ( IF(BOUND(?numRanges), ?numRanges, 0) AS ?numberOfRanges).
      FILTER (?numRanges > 1 || ?numDomains > 1)
    }
  }
  # create an error message containing the number of domains/ranges as well as the list
  BIND( IF(?numberOfRanges> 1 && ?numberOfDomains > 1 , CONCAT("has the ", STR(?numberOfDomains), " defined domains [", STR(?domains), "] and the ", STR(?numberOfRanges), " defined ranges [", STR(?ranges), "]"),
      IF(?numberOfDomains > 1, CONCAT("has the ", STR(?numberOfDomains), " defined domains [", STR(?domains), "]"), CONCAT("has the ", STR(?numberOfRanges), " defined ranges [", STR(?ranges), "]"))) AS ?error).
}
