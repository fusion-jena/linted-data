#
# Copyright 2022 Merle Gänßinger (merle.gaenssinger@uni-jena.de)
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?container ?containerMembershipProperty ?item ?error WHERE {
  {
    ?container a rdf:Seq .
  } UNION {
    ?container a rdf:Bag .
  } UNION {
    ?container a rdf:Alt .
  }
  {
    BIND("Duplicated Index" AS ?error)
    ?container ?containerMembershipProperty ?item .
    FILTER strstarts(str(?containerMembershipProperty),"http://www.w3.org/1999/02/22-rdf-syntax-ns#_")
    FILTER EXISTS {
      ?container ?containerMembershipProperty ?item2 .
      FILTER (?item!=?item2)
    }
  } UNION {
    BIND("Missing Predecessor" AS ?error)
    ?container ?containerMembershipProperty ?item .
    BIND(xsd:integer(STRAFTER(STR(?containerMembershipProperty),"http://www.w3.org/1999/02/22-rdf-syntax-ns#_")) - 1 AS ?index)
    BIND(IRI(CONCAT("http://www.w3.org/1999/02/22-rdf-syntax-ns#_", STR(?index))) AS ?previousContainerMembershipProperty)
    FILTER (?index >= 1)
    FILTER NOT EXISTS {
      ?container ?previousContainerMembershipProperty ?item3
    }
  } UNION {
    BIND("Illegal Index" AS ?error)
    ?container ?containerMembershipProperty ?item .
    BIND(xsd:integer(STRAFTER(STR(?containerMembershipProperty),"http://www.w3.org/1999/02/22-rdf-syntax-ns#_")) AS ?index)
    FILTER (?index < 1)
  }
}
ORDER BY ?container ?containerMembershipProperty ?item
